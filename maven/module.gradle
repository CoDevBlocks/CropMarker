apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives androidSourcesJar
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId = rootProject.group
                artifactId = rootProject.name
                version = rootProject.version

                artifact bundleReleaseAar
                artifact androidSourcesJar

                pom {
                    name = "CropMarker"
                    description = "A crop marker overlay view with support to show, adjust and compute a crop area"
                    url = "https://github.com/CoDevBlocks/CropMarker"

                    licenses {
                        license {
                            name = "The Apache License, Version 2.0"
                            url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                        }
                    }

                    developers {
                        developer {
                            id = "Cosmin-CoDevBlocks"
                            name = "Cosmin Radu"
                            email = "cosmin@codevblocks.com"
                        }
                    }

                    scm {
                        connection = "scm:git:git://github.com/CoDevBlocks/CropMarker.git"
                        developerConnection = "scm:git:ssh://github.com/CoDevBlocks/CropMarker.git"
                        url = "https://github.com/CoDevBlocks/CropMarker"
                    }
                }
            }
        }
    }
}

signing {
    if (rootProject.hasProperty("signing.keyId")
            && rootProject.hasProperty("signing.password")
            && rootProject.hasProperty("signing.secretKeyRingFile")) {
        sign publishing.publications
    } else {
        throw new GradleException("!! ERROR: Signing properties NOT set. Cannot sign artifacts. !!")
    }
}

